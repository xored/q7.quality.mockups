--- Q7 testcase ---
Format-Version: 1.0
Q7-vendor: www.xored.com/q7
Context-Type: com.xored.q7.ecl.context
Element-Name: New Java Class Procs
Element-Type: context
Element-Version: 2.0
Id: _s8Td8ArsEeO-ZLS9uD0OEw
Q7-Runtime-Version: 1.3.6.201308021142
Save-Time: 8/22/13 12:35 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "set-text-after-label" [val parent -input] [val label] [val text ""] {
	$parent | get-editbox -after [get-label $label] | set-text $text
}

proc "fill-new-class" [val window -input] 
				[val name] 
				[val package ""] 
				[val generateMain [bool false]] {
	with $window {
		set-text-after-label $nameLabel $name
		set-text-after-label $packageLabel $package
		if $generateMain {
			get-button $generateMainBox | check
		} -else {
			get-button $generateMainBox | uncheck
		}
		
		get-button Finish | click
	}
}


proc "open-new-class-dialog" [val project] [val package] {
	let [val path [format "%s/src/%s" $project $package]]
		[val tree [get-view $explorerView | get-tree]] {
		$tree | select $path | get-menu $newClassMenu | click
	}	
}

proc "create-java-class" [val project] [val package] [val name] [val generateMain [bool false]] {
	open-new-class-dialog $project $package 
	get-window $newClassTitle | fill-new-class $name $package $generateMain
}



------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
